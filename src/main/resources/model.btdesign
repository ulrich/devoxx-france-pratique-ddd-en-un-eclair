Application MyApp {
   basePackage=org.uva.devoxx

  /** 
   * DOMAINE DE LA PRESENTATION :
   * 
   * Une présentation pour Devoxx est l'action de présenter un sujet autour du monde Java et l'indistrualisation de l'informatique.
   * 
   * Le sujet de la présentation tournera autour des thèmes suivant :
   *    - Le Web,
   *    - Le mobile,
   *    - Les langages alternatifs,
   *    - Les pratiques d'entreprises,
   *    - Java et JEE et ses architectures.
   * 
   * Le présentateur pourra choisir son format de présentation soit en français, soit en anglais parmi les choix suivants :
   *    - La conférence,
   *    - L'université,
   *    - Les outils en action,
   *    - Sur le pouce,
   *    - ...
   * 
   * Le présentateur aura la possibilité de soumettre au maximum trois présentations et ce jusqu'au 01/03/2012.
   * Il pourra éventuellement indiquer le type d'audience intéressé par la présentation.
   */

   Module cfp {

      Resource PresentationResource {
         String createForm;

         show GET path="/presentation/{id}" return="/presentation/show" => PresentationService.findById;
         showAll GET path="/presentation" return="/presentation/showlist" => PresentationService.findAll;

         searchBySujet GET path="/presentation/search" return="/presentation/search";
         showBySujet GET path="/presentation/searchsujet" return="/presentation/showlist" => PresentationService.findBySujet;

         create => PresentationService.save;
         delete return="redirect:../presentation" => PresentationService.delete;
      }

      Service PresentationService {
         save => PresentationRepository.save;
         delete => PresentationRepository.delete;
         findAll => PresentationRepository.findAll;
         findById => PresentationRepository.findById;         

         findBySujet => PresentationRepository.findBySujet;
      }

      Entity Presentation {
         Integer id key;
         - @Sujet sujet;
         - @Format format;
         - @Audience audience nullable;
         - @Langue langue nullable;

         Repository PresentationRepository {
            save;
            delete;
            findAll;
            findById;
            protected findByExample;

            List<@Presentation> findBySujet(String terme);
         }
      }

      BasicType Sujet {
         not immutable
         String titre;
         String resume;
        - @Theme theme;
      }

      enum Theme {
         WEB,
         MOBILE,
         LANGAGES_ALTERNATIFS,
         PRATIQUE_ENTREPRISE,
         JAVA_JEE_ARCHITECTURE;
      } 

      enum Format {
         Integer temps;

         UNIVERSITE(240), CONFERENCE(60), OUTILS_EN_ACTION(30), SUR_LE_POUCE(15);
      }

      enum Audience {
         DEBUTANT,
         CONFIRME,
         EXPERT;
      }

      enum Langue {
         FR,
         EN;
      }

   }
    
}
